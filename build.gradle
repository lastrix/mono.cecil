buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
    }
}

plugins {
    id 'java'
    id 'antlr'
    id 'signing'
}

apply plugin: 'com.bmuschko.nexus'

group 'com.github.lastrix'
version '0.10.0.0'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    // https://mvnrepository.com/artifact/org.jetbrains/annotations
    compile group: 'org.jetbrains', name: 'annotations', version: '16.0.3'
    // https://mvnrepository.com/artifact/org.apache.commons/commons-lang3
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.4'
    // https://mvnrepository.com/artifact/org.slf4j/slf4j-api
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
    // https://mvnrepository.com/artifact/commons-codec/commons-codec
    compile group: 'commons-codec', name: 'commons-codec', version: '1.10'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    // https://mvnrepository.com/artifact/org.projectlombok/lombok
    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.16.20'
}

extraArchive {
    nexus {
        sign = true
    }
}

jar {
    manifest {
        attributes(
                'Implementation-Title': 'Mono.Cecil (originally from C#)',
                'Implementation-Version': version,
                "Class-Path": configurations.compile.collect { it.getName() }.join(' '))
    }
}


modifyPom {
    project {
        name 'mono.cecil'
        description 'Almost exact copy of c# mono.cecil library.'
        url 'https://github.com/lastrix/mono.cecil'
        inceptionYear '2018'

        scm {
            url 'https://github.com/lastrix/mono.cecil'
            connection 'scm:https://lastrix@github.com/lastrix/mono.cecil.git'
            developerConnection 'scm:git://github.com/lastrix/mono.cecil.git'
        }

        licenses {
            license {
                name 'MIT License'
                url 'http://www.opensource.org/licenses/mit-license.php'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'lastrix'
                name 'Lapinin Sergey'
                email '2@lastrix.org'
            }
        }
    }
}